CREATE OR REPLACE FUNCTION DIVIDA.CALCULO_CRC16(V_STRING VARCHAR2) RETURN VARCHAR2 IS

  CURSOR C_BYTES (PIX_STRING VARCHAR2) IS
    SELECT TO_NUMBER(COLUMN_VALUE) AS BYTES
      FROM XMLTABLE(SUBSTR(DUMP(PIX_STRING),INSTR(DUMP(PIX_STRING),':')+1));

  C NUMBER;
  J NUMBER;

  V_RESULTADO VARCHAR2(4000) := 'FFFF'; -- Valor inicial: 0xFFFF
  V_POLINOMIO VARCHAR2(4) := '1021'; -- Polinômio: 0x1021

  V_VERIFICA NUMBER;

BEGIN
  FOR R_BYTES IN C_BYTES(V_STRING)
  LOOP
    C := R_BYTES.BYTES;

    IF (C_BYTES%ROWCOUNT = 1) THEN
      V_RESULTADO := TO_NUMBER(V_RESULTADO, 'XXXX');
    END IF;

    J := (C * POWER(2,8));
    V_RESULTADO := (J + V_RESULTADO) - BITAND(J, V_RESULTADO) * 2;

    FOR BITWISE IN 0..7 LOOP
      V_RESULTADO := V_RESULTADO * POWER(2,1);
      V_VERIFICA := BITAND(V_RESULTADO, TO_NUMBER('10000', 'XXXXX'));

      IF (V_VERIFICA <> 0) THEN
        V_RESULTADO := (V_RESULTADO + TO_NUMBER(V_POLINOMIO, 'XXXX')) - BITAND(V_RESULTADO, TO_NUMBER(V_POLINOMIO, 'XXXX')) * 2;
      END IF;

      V_RESULTADO := BITAND(V_RESULTADO,  TO_NUMBER('FFFF', 'XXXX'));
    END LOOP;
  END LOOP;

  RETURN LPAD(TRIM(TO_CHAR(V_RESULTADO, 'XXXX')),4,'0');

EXCEPTION
  WHEN OTHERS THEN
	  RAISE_APPLICATION_ERROR(-20999,'Erro ao calcular o CRC16: '||SQLERRM);
END CALCULO_CRC16;
/
